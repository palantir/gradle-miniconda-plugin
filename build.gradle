// Copyright 2015 Palantir Technologies
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

plugins {
    id 'idea'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.9.4'
    id 'com.palantir.git-version' version '0.3.0'
}

project.version = gitVersion()
project.group = 'com.palantir.python'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
    jcenter()
}

// See https://docs.gradle.org/2.6/userguide/test_kit.html
// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile gradleApi()

    testCompile gradleTestKit()
    testCompile('com.netflix.nebula:nebula-test:4.0.0') {
        exclude group: 'org.codehaus.groovy'
    }

    testRuntime files(createClasspathManifest)
}

pluginBundle {
    website = 'https://github.com/palantir/gradle-miniconda-plugin'
    vcsUrl = 'https://github.com/palantir/gradle-miniconda-plugin'
    description = 'Gradle plugin to set up Miniconda-based Python build environment'
    tags = ['python', 'miniconda']
    plugins {
        minicondaPlugin {
            id = 'com.palantir.python.miniconda'
            displayName = 'Gradle Miniconda plugin'
        }
    }
}

test {
    maxParallelForks = 10
    forkEvery = 1
}
